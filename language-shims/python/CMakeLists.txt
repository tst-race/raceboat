
# 
# Copyright 2023 Two Six Technologies
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#     http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# 

################################################################################
# Setup SWIG
################################################################################

cmake_policy(SET CMP0078 NEW)
cmake_policy(SET CMP0086 NEW)
find_package(SWIG REQUIRED COMPONENTS python)
set(UseSWIG_MODULE_VERSION 2)
include(${SWIG_USE_FILE})

find_package(PythonLibs 3.7 EXACT REQUIRED)

set(CMAKE_SWIG_FLAGS "-DSWIGWORDSIZE64")
set(SWIG_COMPILE_FLAGS "${SWIG_COMPILE_FLAGS} -DSWIG_TYPE_TABLE=RaceSdk")


################################################################################
# Shims headers
################################################################################

set(SWIG_PYRUN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/swigpyrun.h)

set(NETWORK_MANAGER_PY_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/networkManagerPluginBindingsPYTHON_wrap.h)
set(COMMS_PY_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/commsPluginBindingsPYTHON_wrap.h)

# Generate swigpyrun.h
add_custom_command(
  COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/include
  COMMAND ${SWIG_EXECUTABLE} -Wall -c++ -python -external-runtime ${SWIG_PYRUN_HEADER}
  OUTPUT ${SWIG_PYRUN_HEADER}
  COMMENT "Generating swigpyrun.h"
)

add_custom_target(swigpyrunHeader DEPENDS ${SWIG_PYRUN_HEADER})

# Create a target that other targets may used to include the swigpyrun header / other headers generated by swig
add_library(python-shims-headers INTERFACE)
add_dependencies(python-shims-headers swigpyrunHeader)
# Specify these headers as SYSTEM headers so they don't generate warnings in consuming targets
target_include_directories(python-shims-headers SYSTEM INTERFACE
  $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>
  $<INSTALL_INTERFACE:include>
)

set_target_properties(python-shims-headers PROPERTIES
  PUBLIC_HEADER "${SWIG_PYRUN_HEADER};${NETWORK_MANAGER_PY_HEADER};${COMMS_PY_HEADER}"
)

# ################################################################################
# # Python Comms
# ################################################################################

set_source_files_properties(commsPluginBindings.i PROPERTIES CPLUSPLUS ON)
swig_add_library(commsPluginBindings TYPE SHARED
  LANGUAGE python
  SOURCES commsPluginBindings.i
  OUTPUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/"
)
add_dependencies(commsPluginBindings raceSdkCommon)
target_compile_definitions(commsPluginBindings PUBLIC "SWIG_TYPE_TABLE=RaceSdk")
set_target_properties(commsPluginBindings PROPERTIES
  SWIG_USE_TARGET_INCLUDE_DIRECTORIES TRUE
  COMPILE_OPTIONS "-Wno-error" # Clear warnings set globally via add_compile_options
)
target_include_directories(commsPluginBindings PUBLIC ${PYTHON_INCLUDE_PATH})
target_link_options(commsPluginBindings PUBLIC -Wl,--no-undefined)
swig_link_libraries(commsPluginBindings ${PYTHON_LIBRARIES} python-shims-headers raceSdkCommon)

if(CMAKE_ANDROID_ASSETS_DIRECTORIES)
  add_custom_command(
    TARGET commsPluginBindings
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory
          "${CMAKE_ANDROID_ASSETS_DIRECTORIES}/python"
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
          "$<TARGET_FILE:commsPluginBindings>"
          "${CMAKE_ANDROID_ASSETS_DIRECTORIES}/python/$<TARGET_FILE_NAME:commsPluginBindings>"
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
          "${CMAKE_CURRENT_BINARY_DIR}/commsPluginBindings.py"
          "${CMAKE_ANDROID_ASSETS_DIRECTORIES}/python/commsPluginBindings.py"
    COMMENT "Copy commsPluginBindings to assets"
  )
endif()

# ################################################################################
# # Install
# ################################################################################

# Comm Python bindings
install(
  TARGETS commsPluginBindings
  LIBRARY DESTINATION lib/python
)
install(
  FILES ${CMAKE_CURRENT_BINARY_DIR}/commsPluginBindings.py
  DESTINATION lib/python
)

